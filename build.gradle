apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.diffplug.spotless'

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.29.0'
        classpath 'com.diffplug.spotless:spotless-plugin-gradle:5.2.0'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        enforceCheck false
        java {
            target '**/*.java'
            trimTrailingWhitespace()
            indentWithSpaces(4)
            endWithNewline()
            removeUnusedImports()
        }
    }
}

clean {
    delete rootProject.buildDir
}

//Configuration for gradle-versions-plugin to avoid non-release versions
//to run: gradlew dependencyUpdates
dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
